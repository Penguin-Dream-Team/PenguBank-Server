buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'

group 'club.pengubank'
version '0.1.5'
mainClassName = 'ApplicationKt'

task copyResources {
    project.copy {
        from("$projectDir/resources") {
            exclude("**/*.example")
            exclude("**/logback.xml")
        }
        into("${rootProject.buildDir}/install/${project.name}/resources")
        rename("logback.deploy.xml", "logback.xml")
    }
}

task stage {
    dependsOn copyResources
    dependsOn installDist
}

task cleanPackageDistribution(type: Delete) {
    delete files("dist")
}

task packageDistribution(type: Zip) {
    dependsOn cleanPackageDistribution
    dependsOn stage

    archiveFileName = "${project.name}.zip"
    destinationDirectory = file("dist")

    from "$buildDir/install/${project.name}"
}

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    main.resources.srcDirs = ['resources']
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.apache.httpcomponents:httpclient:4.5.3"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-locations:$ktor_version"
    implementation "io.ktor:ktor-server-host-common:$ktor_version"
    implementation "io.ktor:ktor-auth:$ktor_version"
    implementation "io.ktor:ktor-auth-jwt:$ktor_version"
    implementation "io.ktor:ktor-gson:$ktor_version"
    implementation "org.kodein.di:kodein-di-framework-ktor-server-jvm:7.1.0" // kodein for ktor
    // html dsl
    compile "io.ktor:ktor-html-builder:$ktor_version"

    implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-dao:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jodatime:$exposed_version"

    implementation "com.zaxxer:HikariCP:3.4.5" // JDBC Connection Pool
    implementation "org.postgresql:postgresql:42.2.1" // JDBC Connector for PostgreSQL

    implementation "org.springframework.security:spring-security-core:5.4.1" // BCrypt password encoder
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
